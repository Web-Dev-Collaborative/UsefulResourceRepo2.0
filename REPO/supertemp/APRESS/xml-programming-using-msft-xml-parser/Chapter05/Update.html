<html>
  <head>
    <script language="VBScript">
      <!--

      Dim rsClient
      Dim xmlDOM
      Dim xmlstream

      Sub sendToServer_onClick
         Dim xmlhttp
         Dim stream

         if ListButtonArea.innerHTML = "" then
            'Prepares a Stream object to transmit Recordset content
            Set stream = CreateObject("ADODB.Stream")
            stream.Mode = adModeRead  'adModeRead=1
            stream.Open               'instantiates a stream

            rsClient.Save stream, adpersistADTG
            rsClient.Close

            Set xmlhttp = CreateObject ("MSXML2.XMLHTTP")
            xmlhttp.Open "POST", "BooksServer.asp?req=update", false
            xmlhttp.Send stream.Read(stream.Size)

            'Displays response from server
            BooksArea.innerHTML = xmlhttp.responseText

            'Clears the rest of the buttons
            UpdateButtonArea.innerHTML = ""
            SendButtonArea.innerHTML = ""
         end if
      End Sub



      Sub getBooks_onClick
         Dim xmlDOM
         Dim xmlhttp

         Set rsClient = CreateObject("ADODB.Recordset")
         Set xmlDOM = CreateObject("MSXML2.DOMDocument")
         Set xmlhttp = CreateObject("MSXML2.XMLHTTP")

         'Initiates a request to BooksServer.asp via XMLHTTP object
         xmlhttp.Open "GET", "BooksServer.asp?req=getBooks", false
         xmlhttp.Send

         'Loads returned stream (via xmlhttp.responseText) into DOM document
         xmlDOM.LoadXML xmlhttp.responseText

         rsClient.Open xmlDOM
         displayBooks
      End Sub


      Sub displayBooks
         displayString = "<p><table border='1' cellpadding='7'>" &_
                         "<tr><th>ISBN</th> <th>Price</th></tr>"

         while not rsClient.EOF
            displayString = displayString &_
                            "<tr><td>" & rsClient("ISBN") & "</td>" &_
                            "<td>" & rsClient("Price") & "</td></tr>"
            rsClient.MoveNext
         wend

         displayString = displayString & "</table>"
         BooksArea.innerHTML = displayString
         ListButtonArea.innerHTML = ""
      End Sub


      Sub updatePrice_onClick
         if ListButtonArea.innerHTML = "" then
            'Update prices in the record set, rsClient
            rsClient.MoveFirst
            while not rsClient.EOF
               if rsClient("Price") > 40 then
                  rsClient("Price") = rsClient("Price") - 0.5
               end if
               rsClient.MoveNext
            wend
            rsClient.MoveFirst
            displayBooks
         end if
      End Sub

      -->
    </script>
  </head>

  <body>
    <h3>Granting Discount to Books</h3>
    <div id="BooksArea"></div>
    <table border="0">
      <tr>
        <td width="200">
          <div id="ListButtonArea">
            <button name="getBooks">List Books</button>
          </div>
        </td>
        <td><button name="updatePrice">- 50&cent;</button></td>
        <td><button name="sendToServer">Submit</button></td>
      </tr>
    </table>
  </body>
</html>
